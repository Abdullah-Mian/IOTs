#include <SPI.h>
#include <RF24.h>

// Joystick pins
#define X_AXIS A7
#define Y_AXIS A6
#define BUTTON_PIN 2

RF24 radio(9, 10); // CE = D9, CSN = D10
const byte address[6] = "00001"; // Address for communication

char lastSent = '\0'; // To keep track of last sent character

void setup() {
  pinMode(X_AXIS, INPUT);
  pinMode(Y_AXIS, INPUT);
  pinMode(BUTTON_PIN, INPUT_PULLUP);
  Serial.begin(9600);
  radio.begin();
  if (radio.isChipConnected()) {
    Serial.println("NRF24L01 connected");
  } else {
    Serial.println("NRF24L01 not connected");
  }
  radio.openWritingPipe(address);
  radio.setPALevel(RF24_PA_LOW);
  radio.stopListening(); // Set to transmitter mode
}

void loop() {
  int xValue = analogRead(X_AXIS);
  int yValue = analogRead(Y_AXIS);
  bool buttonPressed = digitalRead(BUTTON_PIN) == LOW;

  char direction = '\0';

  if (yValue < 400) {
    direction = 'W'; // Forward
  } else if (yValue > 600) {
    direction = 'B'; // Backward
  } else if (xValue < 400) {
    direction = 'L'; // Left
  } else if (xValue > 600) {
    direction = 'R'; // Right
  } else {
    direction = 'S';
  }

  // Only send if direction changed and it's valid
  if (direction != lastSent) {
    radio.write(&direction, 1);
    Serial.print("Sent: ");
    Serial.println(direction);
    lastSent = direction;
  }

  delay(100); // Delay for readability
}